sinfoindex = 0
sinfowait = 0
sinforetry = ""
sinfopass = ""
sinfonum = 0
sinfodisp = 0
sinfoplayers = 0
sinfoservers = 0
sinfonumsrv = 0
sinfouitime = 0

sinfotypes = [ "" "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" ]
sinfomodify = [
    sinfotlist = $arg1
    sinfotabsl = (? (> $sinfotlist 0) $sinfotlist (- 0 $sinfotlist))
    loop i (listlen $serversort) [
        sinfostype = (at $serversort $i)
        if $sinfostype [
            sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
            if (!= $sinfotabsl $sinfosabsl) [ append sinfotlist $sinfostype ]
        ]
    ]
    serversort $sinfotlist
]


servermenuinit = [
    sinfoindex = 0
    sinfowait = 0
    sinforetry = ""
    sinfopass = ""
    sinfoplayers = 0
    sinfoservers = 0
    sinfonumsrv = 0
    sinfouitime = (getmillis 1)
    shownservergui = 0
    updateservergui = 0
]
servermenuiter = [
    if (! $shownservergui) [
        maplist = ""
        mappath = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $maplist [=s $mcurmap $lcurmap]) 0) [
                append maplist $lcurmap
                append mappath [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append maplist $lcurmap
                append mappath [base/@lcurmap]
            ]
        ]
        shownservergui = 1
    ]
]
servermenu = [
    sinfopause = 0
    sinfotimer = (- (getmillis 1) $sinfouitime)
    guipage sinfo 5 96 4.2 [getserver] (getserver) [
        sinfostat = (getserver $i 0 0)
        sinfoname = (getserver $i 0 1)
        sinfoport = (getserver $i 0 2)
        sinfonpid = [@[sinfoname]:[@@sinfoport]]
        sinfodesc = (getserver $i 0 3)
        sinfomapn = (getserver $i 0 4)
        sinfonump = (getserver $i 0 5)
        sinfoplayers = (? $i (+ $sinfoplayers $sinfonump) $sinfonump)
        sinfoservers = (? $i (? $sinfonump (+ $sinfoservers 1) $sinfoservers) (? $sinfonump 1 0))
        sinfoping = (getserver $i 0 6)
        sinfolast = (getserver $i 0 7)
        sinfogver = (getserver $i 1 0)
        sinfomode = (getserver $i 1 1)
        sinfomuts = (getserver $i 1 2)
        sinfotime = (getserver $i 1 3)
        sinfomaxp = (getserver $i 1 4)
        sinfomstr = (getserver $i 1 5)
        sinfovars = (getserver $i 1 6)
        sinfomods = (getserver $i 1 7)
        sinfoofft = (? (>= $sinfolast 0) (div (max (- (getmillis 1) (- $sinfolast (div $sinfoping 2))) 0) 1000) 0)
        sinfoactive = (? (< $sinfoping $serverwaiting) 1 0)
        sinfonumsrv = (? $i (+ $sinfonumsrv $sinfoactive) $sinfoactive)
    ] [1] [
        if $updateservergui [
            guibutton "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ updateservergui = 1; updatefrommaster; sinfouitime = (getmillis) ]
        ]
    ] [
        if (getserver) [ sleep 1 [ updateservers ] ]
        guilist [
            guibutton "^fgupdate"			updatefrommaster;	guistrut 1.5
            guibutton "^frreset"			clearservers;		guistrut 1.5
            guibutton "^fodisconnect"		disconnect;			guistrut 1.5
            guibutton "^fmlan connect"		lanconnect;			guistrut 1.5
            guicheckbox "^fcsearch lan"		searchlan;			guistrut 1.5
            guicheckbox (? $pausesortservers "^fdauto sort" "^fbauto sort")		autosortservers;	guistrut 1.5
            guibutton "^fysort now"			sortservers
            guispring
            guitext (format "^fd[ ^fc%1 ^fwplayers on ^fc%2 ^fwof ^fc%3^fa/^fc%4 ^fwserver%5 ^fd]" $sinfoplayers $sinfoservers $sinfonumsrv $sinfonum (? (!= $sinfonum 1) "s"))
            guistrut 1
        ]
        guistrut 0.25
        if (> (getversion 3) (getversion 1)) [
            guilist [ guibutton "^fzoynew version released! ^fwget it now from: ^fcwww.redeclipse.net" "" ]
            guistrut 0.25
        ]
    ] [
        guistrut 0.125
        guimerge 88 [
            guicenter [
                guistrut 10 1
                if $sinfoactive [
                    guicenter [
                        guifont "emphasis" [ guibutton (format "^fw%1" $sinfonump) ]
                        guifont "reduced" [ guibutton (format "^fd / ^fa%1" $sinfomaxp) ]
                    ]
                    guicenter [
                        guifont "reduced" [ guibutton (format "^f%1%2" (? (< $sinfoping 200) "g" (? (< $sinfoping 400) "y" "r")) $sinfoping) ]
                        guifont "little" [ guibutton " ^fams" ]
                    ]
                    guicenter [
                        sinfosecs = (? (>= $sinfolast 0) (div (- (getmillis 1) $sinfolast) 1000) -1)
                        guifont "little" [ guibutton (? (>= $sinfosecs 0) (format "^fa%1 ^fds ago" $sinfosecs) "^fdwaiting..") ]
                    ]
                ] [ guicenter [ guifont "huge" [ guispring; guibutton "?"; guispring ] ] ]
            ]
            sinfoimage = "textures/emblem"
            if (&& [< $sinfoping $serverwaiting] [= $sinfogver (getversion 1)]) [
                sinfopath = (listfind curmap $maplist [|| [=s $curmap $sinfomapn] [=s [base/@curmap] $sinfomapn] [
                    && [> $hasoctapaks 0] [=s [base/@curmap] $sinfomapn]
                ]])
                if (> $sinfopath -1) [ sinfoimage = (at $mappath $sinfopath) ]
            ]
            guiimage $sinfoimage "" 2 1 "textures/emblem"
            guistrut 1
            guicenter [
                guilist [
                    guibutton (format "^fw%1 " $sinfodesc)
                    guifont "little" [ guibutton (format "^fd(^fa%1^fd)" $sinfonpid) ]
                ]
                guispring
                if $sinfoactive [
                    guilist [
                        guistrut 0.25
                        if (= $sinfogver (getversion 1)) [
                            guinohitfx [ guifont "default" [
                                case $sinfostat 0 [
                                    guibutton "^fgopen" [] [] "textures/servers/default"
                                ] 1 [
                                    guibutton "^fylock" [] [] "textures/servers/locked"
                                ] 2 [
                                    guibutton "^fmpriv" [] [] "textures/servers/private"
                                ] 3 [
                                    guibutton "^frfull" [] [] "textures/servers/full"
                                ] () [
                                    guibutton "^founknown" [] [] "textures/servers/unknown"
                                ]
                            ] ]
                            if (!= $sinfomode $modeidxediting) [
                                guifont "reduced" [ guibutton (format " ^fd[^fw%1^fd]" (timestr (? $sinfonump (max (- $sinfotime $sinfoofft) 0) $sinfotime) 2)) ]
                            ]
                            gname = (gamename $sinfomode $sinfomuts 0 32)
                            guifont "little" [
                                guibutton (format " ^fy%1 ^faon ^fo%2" $gname $sinfomapn)
                                guibutton (format " ^fa(%1modified^fa)" (? $sinfomods (format "^fc%1%% " (precf (*f (divf $sinfomods $sinfovars) 100) 2)) "^fgun"))
                            ]
                        ] [
                            guinohitfx [ guifont "default" [ guibutton "^foincompatible" [] [] "textures/servers/failed" ] ]
                            guifont "reduced" [ guibutton (concat " ^faserver is using" (? (> $sinfogver (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol") ]
                        ]
                    ]
                    guispring
                    guilist [
                        if (=s $sinfonpid $sinforetry) [
                            if (= $sinfostat 3) [ guibutton "^fd[ ^fwwaiting for slot ^fd] " ]
                            guibutton "^fwpassword ^fd= "
                            sinfopassval = $sinfopass
                            guifield sinfopassval 20 [sinfopass = $sinfopassval]
                        ] [
                            if (> $sinfonump 0) [
                                guistrut 0.25
                                sinfopnum = (getserver $i 2)
                                if (> $sinfopnum 0) [
                                    //guifont "reduced" [ guibutton (format "^fc%1 ^fwplayer%2: " $sinfopnum (? (> $sinfopnum 1) "s")) ]
                                    guifont "little" [
                                        pname = ""
                                        pmore = 0
                                        loop j $sinfopnum [
                                            if (|| $pmore (> (guitextwidth $pname) 60)) [ pmore = (+ $pmore 1) ] [
                                                append pname (format ["%1"] (getserver $i 2 $j))
                                            ]
                                        ]
                                        guibutton (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                    ]
                                ] [ guifont "reduced" [ guibutton "^faplayer info not available" ] ]
                            ] [ guifont "reduced" [ guibutton "^fano players online" ] ]
                        ]
                    ]
                ] [
                    guilist [
                        guistrut 0.25
                        guifont "default" [ guibutton "^founresponsive" [] [] "textures/servers/failed" ]
                        guifont "reduced" [ guibutton " ^faserver is not replying to queries" ]
                    ]
                ]
            ]
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (|| [hasauthkey 1] [!= @@sinfomstr 4]))
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (hasauthkey 1))
        ] [ sinfopause = 1 ]
        if (=s $sinfonpid $sinforetry) [
            if (= $sinfowait 1) [
                if (strlen $sinfopass) [
                    connect $sinfoname $sinfoport $sinfopass
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $sinfostat 3]) [
                    connect $sinfoname $sinfoport
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ]
        ]
        guistrut 0.125
    ] [
    guistrut 0.5
        guilist [
            guifont "little" [
                guitext "sort: "
                loop i (listlen $serversort) [
                    sinfostype = (at $serversort $i)
                    if $sinfostype [
                        sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
                        sinfosname = (at $sinfotypes $sinfosabsl)
                        guistrut 1
                        guibutton (? (> $sinfostype 0) $sinfosname [-@sinfosname]) [
                            sinfomodify @sinfostype
                        ] [
                            sinfomodify (- 0 @sinfostype)
                        ] "" 0xFFFFFF
                    ]
                ]
                guispring
                loop i (listlen $sinfotypes) [
                    if (&& $i [< (listfind sinfoctype $serversort [= $i (? (> $sinfoctype 0) $sinfoctype (- 0 $sinfoctype))]) 0]) [
                        sinfosname = (at $sinfotypes $i)
                        guistrut 1
                        guibutton $sinfosname [
                            sinfomodify @i
                        ] [
                            sinfomodify (- 0 @i)
                        ] "" 0x666666
                    ]
                ]
                guispring
                guibutton "reset" [serversortreset] [serversortreset] "" 0xFF8888
                guistrut 1
            ]
        ]
        guistrut 0.5
        guilist [
            guifont "little" [ guitext "update interval:" ]
            guistrut 2
            guilist [
                guistrut 88 1
                guilistslider serverupdateinterval "1 2 3 4 5 10 15 20 25 30 35 40 45 50 55 60"
            ]
        ]
        guistrut 0.5
        if (hasauthkey) [
            guilist [
                guitext "user account:"
                guispring 1
                guicheckbox (format "identify as ^fs^fc%1^fS on connect" $accountname) authconnect
                guispring 1
                guifont "little" [ guibutton "(^fs^fredit account^fS)" [showgui profile 4] ]
            ]
        ]
    ]
    if (&& [! $guilayoutpass] [|| (> $sinfotimer 30000) (> $sinfonumsrv (div (* $sinfonum 3) 4))]) [ if $sinfopause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]
    guitip (format "press %1 to open this menu at any time" (dobindsearch "showservers"))
]

newgui servers [
    servermenuiter
    guilist [ servermenu ]
] [
    if (= $guipasses 0) [
        servermenuinit
    ]
]

okguidelines = 0
setpersist okguidelines 1
newgui guidelines [
    guieditor doc/guidelines.txt -80 22
    textinit doc/guidelines.txt doc/guidelines.txt
    textmode 4
    guistrut 0.5
    guifont "emphasis" [
        guilist [
            guibutton "[ ^fs^foi disagree, stay offline^fS ]" [cleargui 1]
            guispring 1
            guibutton "[ ^fs^fgi agree, play online^fS ]" [okguidelines = 1; showgui servers]
            guistrut 3
        ]
    ]
    guistatus "you must read and agree to these guidelines before playing online"
] [ if $okguidelines [cleargui 1] ]
showservers = [ showgui (? $okguidelines servers guidelines) ]
