// standard menu definitions
// standard menu definitions

bind ESCAPE [if (cleargui 1) [] [showgui main]]    // it all starts here

textlogo = [ guifont "super" [ guitext "^frRed Eclipse" ] ]

vardef = [ guitext (format "%1" $arg2); (format "%1val" $arg1) = $$arg1 ]
varfld = [ varname = (format "%1val" $arg1); guifield $varname $arg2 [@arg1 $@varname] ]
varbit = [ varname = (format "%1val" $arg1); guibitfield "" $varname $arg2 [@arg1 $@varname] ]
vardef2 = [ guitext (format "%1" $arg2); (format "%1val1" $arg1) = $(format "%11" $arg1); (format "%1val2" $arg1) = $(format "%12" $arg1) ]
varfld2 = [ guilist [ varname1 = (format "%1val1" $arg1); guifield $varname1 $arg2 [@(format "%11" $arg1) $@varname1]; varname2 = (format "%1val2" $arg1); guifield $varname2 $arg2 [@(format "%12" $arg1) $@varname2] ] ]
varbit2 = [ guilist [ varname1 = (format "%1val1" $arg1); guibitfield "" $varname1 $arg2 [@(format "%11" $arg1) $@varname1]; varname2 = (format "%1val2" $arg1); guibitfield "" $varname2 $arg2 [@(format "%12" $arg1) $@varname2] ] ]
varincr = [ guilist [ varname1 = (format "%1val1" $arg1); guibutton "^fy-" [@(format "%11" $arg1) (- $@varname1 1)]; guifield $varname1 $arg2 [@(format "%11" $arg1) $@varname1]; guibutton "^fg+" [@(format "%11" $arg1) (+ $@varname1 1)]; varname2 = (format "%1val2" $arg1); guifield $varname2 $arg2 [@(format "%12" $arg1) $@varname2] ] ]

guicomb = [ guilist [ guilist [ arg1 ] ] ]
guibox = [
    guilist [ guistrut 70 ]
    guilist [
        guiimage "textures/emblem" $arg1 7.5 1 "textures/nothumb"
        guistrut $arg2
        guilist [
            guistrut 0.5
            textlogo
            guistrut 0.5
            arg3
        ]
        arg4
    ]
]

guimerge = [ guibody [guilist [guilist [guistrut $arg1]; guilist [arg2]]] $arg3 $arg4 ]
guiarea = [
    guilist [
        if (> $numargs 9) [ arg10 ]
        guilist [
            (format "%1count" $arg1) = $arg2
            (format "%1disp" $arg1) = 0
            (format "%1list" $arg1) = 0
            guilist [
                guistrut 0.5
                guilist [ guistrut $arg3 ]
                if (arg5) [
                    (format "%1num" $arg1) = $arg6
                    if (> $(format "%1num" $arg1) 0) [
                        (format "%1index" $arg1) = (min (max 0 (- $(format "%1num" $arg1) $(format "%1count" $arg1))) $(format "%1index" $arg1)) //safeguard
                        loop i $(format "%1num" $arg1) [
                            arg7
                            if (arg8) [
                                if (&& (>= $i $(format "%1index" $arg1)) (< $(format "%1list" $arg1) $(format "%1count" $arg1))) [
                                    arg11
                                    (format "%1list" $arg1) = (+ $(format "%1list" $arg1) 1)
                                ]
                                (format "%1disp" $arg1) = (+ $(format "%1disp" $arg1) 1)
                            ]
                        ]
                    ] [ guistrut (-f (*f $arg4 $arg2) 1); arg9; (format "%1list" $arg1) = $arg2 ]
                ] [ guistrut (-f (*f $arg4 $arg2) 1); arg9; (format "%1list" $arg1) = $arg2 ]
                (format "%1alts" $arg1) = (- $(format "%1count" $arg1) $(format "%1list" $arg1))
                if (> $(format "%1alts" $arg1) 0) [ guistrut (*f $(format "%1alts" $arg1) $arg4) ]
            ]
            guistrut 2
            guislider (format "%1index" $arg1) 0 (max (- $(format "%1disp" $arg1) $(format "%1count" $arg1)) 0) [] 1 1
        ]
        if (> $numargs 11) [ arg12 ]
    ]
]
guipage = [ guistayopen [guiarea $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 $arg7 $arg8 $arg9 $arg10 $arg11 $arg12] ]
guicontainer = [ guilist [guilist [if (arg1) [arg2] [arg3]]] ]

tipstr = (showtip)
guitip = [
    if (> $numargs 0) [ tipstr = $arg1 ] [ tipstr = (showtip) ]
    guistayopen [ guibody [
        guistrut 0.5
        guitext "TIP: "
        guifont "radar" [ guitext (format "^fa%1" $tipstr) ]
    ] [ lasttip = 0 ] [ lasttip = 0 ] ]
]

newgui main [
    guicomb [
        guicomb [ guitip ]
        guistrut 0.5
        guibox "showgui servers" 3 [
            guifont "default" [
                guibutton "edit profile" "showgui profile"
                guistrut 1
                if (isonline) [
                    guibutton "disconnect" "disconnect"
                    if (> (getvote 0) 0) [ guibutton "show votes" "showgui maps 2" ]
                    guistrut 1
                ]
                guibutton (? (isonline) "find servers" "play online") "showgui servers"
                guibutton (? (isonline) "vote map/mode" "offline practice") "showgui maps 1"
                guistrut 0.5
                guibutton "options" "showgui options"
                guibutton "variables" "showgui vars"
                guibutton "help" "showgui help"
                guistrut 0.5
                if (> (ircconns) 0) [ guibutton "irc windows" "showgui irc"; guistrut 1 ]
                guibutton "quit" "quit"
            ]
        ] [ guistrut 14 ]
        guistrut 0.5
        guilist [
            guistrut 0.5
            guilist [
                guifont "radar" [
                    cases (? (strlen $guirolloverimgaction) $guirolloverimgaction $guirolloveraction) "showgui servers" [
                        guitext "^fadisplays the server browser for other online matches"
                    ] "showgui profile" [
                        guitext "^faedit your name and colour"
                    ] "showgui irc" [
                        guitext "^fashows your open irc windows"
                    ]  "disconnect" [
                        guitext "^fadisconnects your current connection"
                    ] "showgui maps 2" [
                        guitext "^fadisplays the current votes"
                    ] "showgui maps 1" [
                        guitext (format "^faselect a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
                    ] "showgui options" [
                        guitext "^faedit your configuration options"
                    ] "showgui vars" [
                        guitext "^faedit gameplay and weapon variables"
                    ] "showgui help" [
                        guitext "^fagets some help!"
                    ] "quit" [
                        guitext "^facloses Red Eclipse"
                    ] () [
                        guitext "^faclick on an activity to start"
                    ]
                ]
            ]
        ]
    ]
]

modedescs = [
    ""
    "create and edit existing maps"
    "make your way through a maze of monsters"
    "just shoot to kill and earn points by fragging"
    "grab the enemy flag and return it to your base"
    "secure and defend flags to keep them your colour"
    "get the bomb to the enemy goal before it blows up"
    "compete for the fastest time completing a lap"
]
mutsdescs = [
    "multi: four teams instead of two"
    "team: fight on teams to battle it out"
    "instagib: one shot kills"
    "medieval: spawn only with swords"
    "ballistic: spawn only with rockets"
    "duel: 1 on 1 gameplay where combatants take turns"
    "survivor: last combatant left alive wins"
    "arena: spawn with a choice of two weapons"
    "onslaught: fight waves of monsters as well as combatants"
    "hover: combatants come equipped with a hoverpack"
    "jetpack: combatants come equipped with a jetpack"
    "vampire: damage dealt to combatants regenerates the attacker"
    "expert: headshot damage only"
    "resize: combatants resize depending on their health"
]

guimodes = [
    guilist [
        guistrut 0.5
        guilist [
            guifont "sub" [ guitext (at $modedescs $arg1) ]
            if (> $arg2 0) [
                guistrut 0.25
                guifont "radar" [
                    loop i (listlen $mutsdescs) [
                        if (& $arg2 (<< 1 $i)) [ guitext (concatword "^fa" (at $mutsdescs $i)) ]
                    ]
                ]
            ]
        ]
    ]
]
newgui loading [
    guicomb [
        guicomb [ guitip ]
        guistrut 0.5
        guilist [
            guiimage "" [] 7.5 1
            guistrut 2
            guilist [
                guistrut 1
                textlogo
                guistrut 1
                if (&& (gamemode) (strlen $maptitle) (< (waiting) 2)) [
                    guifont "default" [
                        if (&& (gamemode) (strlen $maptitle)) [ guitext $maptitle ] [ guitext $progresstitle ]
                    ]
                    guifont "sub" [
                        guilist [
                            guistrut 1
                            if (&& (gamemode) (strlen $mapauthor)) [ guitext (format "^faby %1" $mapauthor) ] [ guitext $progresstext ]
                        ]
                    ]
                ] [
                    guifont "default" [ guitext $progresstitle ]
                    guifont "sub" [ guilist [ guistrut 1; guitext $progresstext ] ]
                ]
                guifont "radar" [
                    guilist [
                        guistrut 2
                        if (&& (gamemode) (strlen $maptitle) (< (waiting) 2)) [
                            gname = (gamename (gamemode) (mutators))
                            if (> (strlen $gname) 32) [ gname = (gamename (gamemode) (mutators) 1) ]
                            guitext (format "playing: [ ^fs^fa%1^fS ]" $gname)
                        ] [ guitext " " ]
                    ]
                ]
                if (strlen $connectname) [
                    guifont "radar" [
                        guilist [
                            guistrut 2
                            guitext (format "^faon: ^fw%1:[%2]" $connectname $connectport)
                        ]
                    ]
                    guistrut 1
                ] [ guistrut 2 ]
                guilist [
                    guistrut 32
                    guifont "emphasis" [ guiprogress $progressamt 3 ]
                ]
            ]
        ]
        if (&& (gamemode) (strlen $maptitle) (< (waiting) 2)) [
            guistrut 0.5
            guicomb [ guimodes (gamemode) (mutators) ]
        ]
    ]
]

ircident = 0

newgui irc [
    guistayopen [
        guitext "saved identities"
        guislider ircident 0 10
        guilist [
            guilist [
                guitext "network  "
                guitext "address  "
                guitext "port     "
                guitext "nickname "
                guitext "channel  "
            ]
            guilist [
                guilist [
                    guifield ircname@ircident -40
                    guistrut 1
                    guibutton "connect" [
                        ircaddclient $[ircname@ircident] $[irchost@ircident] $[ircport@ircident] $[ircnick@ircident]
                        ircaddchan $[ircname@ircident] $[ircchan@ircident]
                    ]
                ]
                guifield [irchost@ircident] -40
                guifield [ircport@ircident] -40
                guifield [ircnick@ircident] -40
                guilist [
                    guifield [ircchan@ircident] -40
                    guistrut 1
                    guibutton "join" [ ircaddchan $[ircname@ircident] $[ircchan@ircident] ]
                ]
            ]
        ]
    ]
    guibar
    ircgui
] [
    if (= $guipasses 0) [
        ircname0 = freenode
        irchost0 = irc.freenode.net
        ircport0 = 6667
        ircnick0 = (at (getname) 0)
        ircchan0 = #redeclipse
    ]
]

newgui about [
    guibox "cleargui 1" 3 [
        guifont "default" [ guitext "(C) 2009-2011" ]
        guifont "radar" [
            guitext "Quinton Reeves, Lee Salzman"
            guitext "http://www.redeclipse.net/"
        ]
        guistrut 1
        guitext "Based on Cube Engine 2"
        guifont "default" [ guitext "(C) 2001-2011" ]
        guifont "radar" [
            guitext "Wouter van Oortmerssen, Lee Salzman"
            guitext "  Mike Dysart, Robert Pointon, and Quinton Reeves"
            guitext "http://sauerbraten.org/"
        ]
    ] []
]

newgui support [
    ircgui freenode
] [
    if (= $guipasses 0) [
        ircaddclient freenode irc.freenode.net 6667 (getname)
        ircaddchan freenode #redeclipse
    ]
]

newgui help [
    guibox "cleargui 1" 3 [
        guifont "default" [
            guibutton "about" "showgui about"
            guibutton "live support" "showgui support"
        ]
    ] []
]

newgui test [guilist [loop i 20 [guilist [loop j 20 [textlogo]]]]]

shownmapgui = 0
shownservergui = 0
mapselected = 0
mutsselected = 0
modeselected = 3
mutsbefore = -1
lastmode = -1
lastmuts = -1

newgui profile [
    guibox "cleargui 1" 3 [
        guifont "default" [ guitext "^faPlease enter your details" ]
        guistrut 1
        guilist [
            guitext "name: "
            guifield nameval 24 [setinfo $nameval $colrval]
        ]
        guistrut 1
        guilist [
            colr = (& (>> $colrval 16) 0xFF)
            colg = (& (>> $colrval 8) 0xFF)
            colb = (& $colrval 0xFF)
            guicomb [
                guicomb [
                    guibackground $colrval
                    guistrut 5.5
                    guilist [ guistrut 3 ]
                ]
                guistrut 1
                guilist [
                    guicomb [
                        guilist [ guistrut 25.5 ]
                        guislider colr 0 255 [colrval = (| (| (<< $colr 16) (<< $colg 8)) $colb)] 0 1 0xFF0000
                    ]
                    guistrut 0.15
                    guicomb [
                        guilist [ guistrut 25.5 ]
                        guislider colg 0 255 [colrval = (| (| (<< $colr 16) (<< $colg 8)) $colb)] 0 1 0x00FF00
                    ]
                    guistrut 0.15
                    guicomb [
                        guilist [ guistrut 25.5 ]
                        guislider colb 0 255 [colrval = (| (| (<< $colr 16) (<< $colg 8)) $colb)] 0 1 0x0000FF
                    ]
                ]
            ]
        ]
        guistrut 1
        guilist [
            guistrut 1
            guibutton "^fgok" [setinfo $nameval $colrval; cleargui 1] [] "action"
            guistrut 1
            guibutton "^frcancel" [cleargui 1] [] "exit"
        ]
    ] []
] [
    if (= $guipasses 0) [
        nameval = (getname)
        colrval = (getcolour)
    ]
]

weapname = [ "melee" "pistol" "sword" "shotgun" "smg" "flamer" "plasma" "rifle" "grenade" "rocket" "action" ]

loadweapa = -1
loadweapb = -1
loadweapon = [
    a = $$arg4
    if (&& (!= $a $arg3) (allowedweap $arg3)) [
        guiradio (format "^f[%1]^f(%2)%3" $arg5 $arg6 $arg1) $arg2 $arg3
    ] [
        guilist [
            guilist [ guistrut 0.25; guilist [ guistrut 0.25; guiimage "textures/checkdisable" [] 0.375 0 "textures/blank" ] ]
            guistrut 0.5
            guitext (format "^f[%1]^f(%2)^fe%3" $arg5 $arg6 $arg1)
        ]
    ]
]
newgui loadout [
    guicomb [
        guicomb [ guitip (format "press ^fs^fc%1^fS to open this menu at any time" (searchbinds "showgui loadout" 5 "or" "^fw")) ]
        guibox "cleargui 1" 4 [
            if (& (mutators) (<< 1 7)) [
                guifont "default" [ guitext "choose weapon" ]
                guistrut 1
                guifont "sub" [
                    guilist [
                        guilist [
                            guiradio "^fg^f(textures/question)random" loadweapa 0
                            loadweapon $swordname loadweapa 2 loadweapb $swordcolour "textures/sword"
                            loadweapon $shotgunname loadweapa 3 loadweapb $shotguncolour "textures/shotgun"
                            loadweapon $smgname loadweapa 4 loadweapb $smgcolour "textures/smg"
                            loadweapon $flamername loadweapa 5 loadweapb $flamercolour "textures/flamer"
                            loadweapon $plasmaname loadweapa 6 loadweapb $plasmacolour "textures/plasma"
                            loadweapon $riflename loadweapa 7 loadweapb $riflecolour "textures/rifle"
                            if (& (mutators) 0x1000) [
                                loadweapon $grenadename loadweapa 8 loadweapb $grenadecolour "textures/grenade"
                                loadweapon $rocketname loadweapa 9 loadweapb $rocketcolour "textures/rocket"
                            ]
                        ]
						guistrut 2
						guilist [
							guiradio "^fg^f(textures/question)random" loadweapb 0
							loadweapon $swordname loadweapb 2 loadweapa $swordcolour "textures/sword"
							loadweapon $shotgunname loadweapb 3 loadweapa $shotguncolour "textures/shotgun"
							loadweapon $smgname loadweapb 4 loadweapa $smgcolour "textures/smg"
							loadweapon $flamername loadweapb 5 loadweapa $flamercolour "textures/flamer"
							loadweapon $plasmaname loadweapb 6 loadweapa $plasmacolour "textures/plasma"
							loadweapon $riflename loadweapb 7 loadweapa $riflecolour "textures/rifle"
						]
                    ]
                ]
                guistrut 1
                guilist [
                    guifont "sub" [ guibutton "^frcancel" [cleargui 1] ]
                    guistrut 1
					guifont "sub" [ guistayopen [ guibutton "^fyswap" [ll = $loadweapb; loadweapb = $loadweapa; loadweapa = $ll] ] ]
					guistrut 1
                    guifont "default" [ guibutton "^fgsubmit" [loadweap $loadweapa $loadweapb] ]
                ]
            ] [
                guifont "deafault" [ guitext "sorry" ]
                guistrut 1
                guifont "sub" [ guilist [ guistrut 1; guitext "^frthis is only available with arena" ] ]
                guistrut 1
                guifont "default" [ guibutton "^fgclose" [cleargui 1] ]
            ]
        ] []
    ]
] [
    if (= $guipasses 0) [
        loadweapa = (getloadweap 0)
        loadweapb = (getloadweap 1)
    ]
]

newgui team [
    guicomb [
        guicomb [ guitip (format "press ^fs^fc%1^fS to open this menu at any time" (searchbinds "showgui team" 5 "or" "^fw")) ]
        guibox "cleargui 1" 3 [
            if (&& (> (gamemode) 2) (& (mutators) (<< 1 1))) [
                guifont "default" [ guitext "choose team" ]
                guistrut 1
                guifont "sub" [
                    guibutton (format "^f[%1]%2" $teamalphacolour $teamalphaname) [team 1] [] "teamalpha" $teamalphacolour
                    guibutton (format "^f[%1]%2" $teamomegacolour $teamomeganame) [team 2] [] "teamomega" $teamomegacolour
					if (& (mutators) (<< 1 0)) [
						guibutton (format "^f[%1]%2" $teamkappacolour $teamkappaname) [team 1] [] "teamkappa" $teamkappacolour
						guibutton (format "^f[%1]%2" $teamsigmacolour $teamsigmaname) [team 2] [] "teamsigma" $teamsigmacolour
					]
                ]
                guistrut 1
                guifont "default" [ guibutton "^frcancel" [cleargui 1] [] "action" ]
            ] [
                guifont "default" [ guitext "sorry" ]
                guistrut 1
                guifont "sub" [ guilist [ guistrut 1; guitext "^frthis is only available in team games" ] ]
                guistrut 1
                guifont "default" [ guibutton "^fgclose" [cleargui 1] [] "action" ]
            ]
        ] []
    ]
]

maplists = [ campaignmaps mainmaps capturemaps defendmaps bombermaps trialmaps ]
mapindex = 0
lastmode = -1
lastmuts = -1
mapnum = -1
mapcur = ""
maplist = ""
mappath = ""
mapextra = ""
mapimg = ""
mapocta = 0
shownmapgui = 0

mapsmenuinit = [
    if (< (gamemode) 2) [ modeselected = -1 ] [ modeselected = (gamemode) ]
    if (< (mutators) 0) [ mutsselected = 0 ] [ mutsselected = (mutscheck $modeselected (mutators)) ]
    mapindex = 0
    lastmode = -1
    lastmuts = -1
    mapnum = -1
    mapcur = ""
    maplist = ""
    mappath = ""
    mapextra = ""
    mapimg = ""
    mapocta = 0
    shownmapgui = 0
    mutsbefore = $mutsselected
]
mapsmenuiter = [
    if (&& (>= $modeselected 0) (|| (!= $lastmode $modeselected) (!= $lastmuts $mutsselected) (!= $shownmapgui 1))) [
        mutsselected = (mutscheck $modeselected $mutsselected)
        maplist = ""
        mappath = ""
        if (> $modeselected 1) [
            list = ""
            if (&& (= $modeselected 6) (& $mutsselected 0x2000)) [ list = $holdmaps ] [ list = $(at $maplists (- $modeselected 2)) ]
            if (>= $mapsfilter 1) [
                if (&& (& $mutsselected (<< 1 0)) (|| (= $modeselected 4) (= $modeselected 6))) [ list = (shrinklist $list $multimaps 1) ]
                if (& $mutsselected (<< 1 5)) [ list = (shrinklist $list $duelmaps 1) ]
                if (|| (& $mutsselected (<< 1 9)) (& $mutsselected (<< 1 10))) [ list = (shrinklist $list $hovermaps 1) ]
                if (&& (isonline) (>= $mapsfilter 2) (>= $modeselected 3) (<= $modeselected 6) (= (& $mutsselected (<< 1 5)) 0)) [ 
                    players = (listlen (listclients 1))
                    if (&& (>= $smallmapmax 0) (<= $players $smallmapmax)) [ list = (shrinklist $list $smallmaps) ] [
                        if (&& (>= $mediummapmax 0) (<= $players $mediummapmax)) [ list = (shrinklist $list $mediummaps) ] [
                            if (&& (>= $mediummapmax 0) (> $players $mediummapmax)) [ list = (shrinklist $list $smallmaps) ]
                        ]
                    ]
                ]
            ]
            looplist lcurmap $list [
                maplist = (concat $maplist $lcurmap)
                mappath = (concat $mappath (concatword "maps/" $lcurmap))
            ]
        ]
        mapcount = (listlen $maplist)
        if (!= $modeselected 2) [
            maplist = (concat $maplist "*")
            mappath = (concat $mappath "*");
            loopfiles lcurmap maps mpz [
                if (< (listfind mcurmap $maplist [strcmp $mcurmap $lcurmap]) 0) [
                    maplist = (concat $maplist $lcurmap)
                    mappath = (concat $mappath (concatword "maps/" $lcurmap))
                ]
            ]
            if (&& (> $hasoctapaks 0) (> $mapocta 0)) [
                loopfiles lcurmap base ogz [
                    maplist = (concat $maplist $lcurmap)
                    mappath = (concat $mappath (concatword "base/" $lcurmap))
                ]
            ]
        ]
        maplist = (concat $maplist "?")
        mappath = (concat $mappath "?");
        if (= (strlen $mapcur) 0) [ mapcur = $mapname ]
        mapnum = (listfind curmap $maplist [(|| (strcmp $curmap $mapcur) (strcmp (concatword "maps/" $curmap) $mapcur) (&& (> $hasoctapaks 0) (> $mapocta 0) (strcmp (concatword "base/" $curmap) $mapcur)))])
        if (|| (< $mapnum 0) (>= $mapnum $mapcount)) [ mapnum = (rnd $mapcount) ]
        if (strcmp "*" (at $maplist $mapnum)) [ mapnum = (- (listlen $maplist) 1); mapextra = $mapname ]
        lastmode = $modeselected
        lastmuts = $mutsselected
        mapindex = 0
        shownmapgui = 1
    ]
    if (strcmp (at $maplist $mapnum) "?") [ mapcur = $mapextra; mapimg = $mapextra ] [ mapcur = (at $maplist $mapnum); mapimg = (at $mappath $mapnum) ]
]

mutsvar = [
    if (> (strlen $arg1) 0) [
        guistayopen [ guilist [
            g = 0; g = $$arg2
            m = 0; m = $$arg3
            if (& (mutsimplied $g $m) $arg4) [
                guibutton $arg1 [] [] "checkboxtwo"
            ] [
                if (&& (|| (= $mutslockfilter 0) (= (isonline) 0) (< $modelock 3) (? (= $modelock 4) (isadmin (getclientnum)) (ismaster (getclientnum))) (& $mutslockfilter $arg4)) (& (mutscheck $g (| $m $arg4) $arg4) $arg4)) [
                    if (& $m $arg4) [
                        guibutton $arg1 [
                            m = (mutscheck $g (- $m @arg4))
                            @arg3 = $m
                        ] [] "checkboxon"
                    ] [
                        guibutton $arg1 [
                            m = (mutscheck $g (| $m @arg4) @arg4)
                            @arg3 = $m
                        ] [] "checkbox"
                    ]
                ] [
                    guitext $arg1 "checkdisable"
                ]
            ]
        ] ]
    ] [ guistrut 1 ]
]

modevar = [
    guistayopen [ guilist [
        if (|| (= $modelockfilter 0) (= (isonline) 0) (< $modelock 3) (? (= $modelock 4) (isadmin (getclientnum)) (ismaster (getclientnum))) (& (<< 1 $arg3) $modelockfilter)) [
            g = $$arg2
            if (= $g $arg3) [
                guibutton $arg1 [ @arg2 = @arg3 ] [] "checkboxon"
            ] [
                guibutton $arg1 [ @arg2 = @arg3 ] [] "checkbox"
            ]
        ] [
            guitext $arg1 "checkdisable"
        ]
    ] ]
]

mapsmenu = [
    guilist [
        guilist [ guistrut 74 ]
        guicomb [ guitip (format "press ^fs^fc%1^fS to open this menu at any time" (searchbinds "showgui maps 1" 5 "or" "^fw")) ]
        guistrut 0.5
        guilist [
            guilist [
                guilist [
                    guilist [ guistrut 3 ]
                    guilist [
                        if (< $lastmode 0) [
                            guitext "game select"
                            guifont "radar" [ guitext "^faplease select a mode and map to continue" ]
                        ] [
                            gname = (gamename $modeselected $mutsselected)
                            if (> (strlen $gname) 32) [ gname = (gamename $modeselected $mutsselected 1) ]
                            guitext $gname
                            guilist [ guitext " ^faselected on "; guitext $mapcur ]
                        ]
                    ]
                ]
                guilist [
                    guilist [
                        guitext "gamemode"
                        guistrut 1
                        guifont "radar" [
                            modevar "editing" modeselected 1
                            modevar "campaign" modeselected 2
                            modevar "deathmatch" modeselected 3
                            modevar "capture-the-flag" modeselected 4
                            modevar "defend-the-flag" modeselected 5
                            modevar "bomber-ball" modeselected 6
                            modevar "time-trial" modeselected 7
                        ]
                    ]
                    guistrut 3
                    guilist [
                        guistayopen [
                            guiimage (format "<blur>%1" $mapimg) [showgui maps 2; sleep 100 [ start @@mapcur @@modeselected @mutsselected ]] 5 1 "<blur>textures/emblem"
                        ]
                    ]
                ]
                guistrut 0.5
                guifont "sub" [ guitext "mutators" ]
                guistrut 0.5
                guilist [
                    guifont "radar" [
                        guilist [
                            mutsvar "multi" modeselected mutsselected (<< 1 0)
                            mutsvar "hover" modeselected mutsselected (<< 1 9)
                            mutsvar "onslaught" modeselected mutsselected (<< 1 8)
                            mutsvar "arena" modeselected mutsselected (<< 1 7)
                            mutsvar "resize" modeselected mutsselected (<< 1 13)
                        ]
                        guistrut 3
                        guilist [
                            mutsvar "teamplay" modeselected mutsselected (<< 1 1)
                            mutsvar "instagib" modeselected mutsselected (<< 1 2)
                            mutsvar "jetpack" modeselected mutsselected (<< 1 10)
                            mutsvar "medieval" modeselected mutsselected (<< 1 3)
                            mutsvar "ballistic" modeselected mutsselected (<< 1 4)
                        ]
                        guistrut 3
                        guilist [
                            mutsvar "duel" modeselected mutsselected (<< 1 5)
                            mutsvar "survivor" modeselected mutsselected (<< 1 6)
                            mutsvar "vampire" modeselected mutsselected (<< 1 11)
                            mutsvar "expert" modeselected mutsselected (<< 1 12)
                        ]
                    ]
                ]
                if (> (strlen (gspmutname $modeselected 0)) 0) [
                    guistrut 0.5
                    guifont "radar" [ guitext "mode specific" ]
                    guistrut 0.5
                    guilist [
                        guifont "radar" [
                            guilist [
                                mutsvar (gspmutname $modeselected 0) modeselected mutsselected (<< 1 14)
                            ]
                            guistrut 3
                            guilist [
                                mutsvar (gspmutname $modeselected 1) modeselected mutsselected (<< 1 15)
                            ]
                            guistrut 3
                            guilist [
                                mutsvar (gspmutname $modeselected 2) modeselected mutsselected (<< 1 16)
                            ]
                        ]
                    ]
                ]
                guistrut 1
                guilist [
                    if (> $hasoctapaks 0) [
                        guistayopen [
                            guibutton "show maps from sauerbraten" [
                                if (> $mapocta 0) [mapocta = 0] [mapocta = 1]
                                shownmapgui = 0
                            ] [] (? (> $mapocta 0) "checkboxon" "checkbox")
                        ]
                    ]
                ]
            ]
            guistrut 2.5
            mapscount = 20
            guilist [
                guilist [ guitext "available maps" ]
                guilist [
                    guifont "radar" [
                        guicontainer [>= $lastmode 0] [
                            nummaps = (- (listlen $maplist) 1)
                            guilist [
                                guilist [
                                    guilist [ guistrut $mapscount ]
                                    mapindex = (min (max 0 (- $nummaps $mapscount)) $mapindex) //safeguard
                                    mapnum = (min $mapnum $nummaps)
                                    guilist [
                                        guilist [ guistrut 34 ]
                                        break = 0
                                        loopwhile i $mapscount [if (= $break 0) [ 1 ] [ 0 ]] [
                                            q = (+ $mapindex $i)
                                            curmap = (at $maplist $q)
                                            if (strcmp $curmap "*") [
                                                guifont "radar" [ guitext "maps not in the rotation" ]
                                            ] [
                                                if (strcmp $curmap "?") [ break = 1 ] [ guiradio $curmap mapnum $q ]
                                            ]
                                        ]
                                    ]
                                ]
                                guilist [ guifont "radar" [ guitext "^custom map" ] ]
                                guilist [
                                    guiradio "" mapnum $nummaps
                                    mapextraval = $mapextra
                                    guifield mapextraval 32 [mapextra = $mapextraval]
                                ]
                            ]
                            guistrut 2
                            guislider mapindex 0 (max (- $nummaps $mapscount) 0) [] 1 1
                        ] [ guistrut 38; guilist [ guistrut (+f $mapscount 3.5) ] ]
                    ]
                ]
            ]
        ]
        guistrut 0.5
        guilist [
            if (>= $lastmode 0) [
                guistrut 47.5
                guifont "super" [
                    guistayopen [
                        guibutton (? (isonline) "^fgsubmit vote" "^fgstart game") (format "showgui maps 2; sleep 100 [ start %1 %2 %3 ]" $mapcur $modeselected $mutsselected)
                    ]
                ]
            ] [ guistrut 1 ]
        ]
    ]
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]
votemenu = [
    guipage vote 6 74 4.2 [1] (getvote 0) [vote = (getvote (+ $i 1))] [1] [guibutton "^fwThere are no votes currently pending, ^fgsubmit ^fwone yourself" [showgui maps 1] [] "conopen"] [
        guicomb [ guitip (format "press ^fs^fc%1^fS to open this menu at any time" (searchbinds "showgui maps 2" 5 "or" "^fw")) ]
        guistrut 0.5
        guilist [
            guicheckbox "^fcdynamic sort" sortvotes; guistrut 1
            guicheckbox "^focleanup list" cleanvotes; guistrut 1
            guibutton "^fyclear vote" clearvote; guistrut 1
            guitext (format "^fd[ ^fc%1" $votenum)
            if (= $votenum 1) [ guitext " ^fwvote ^fd]" ] [ guitext " ^fwvotes ^fd]" ]
        ]
    ] [
        voteplayers = (at $vote 0)
        votemode = (at $vote 1)
        votemuts = (at $vote 2)
        votemap = (at $vote 3)
        votegame = (at $vote 1)
        voteself = 0
        loop j $voteplayers [ if (= (getclientnum) (getvote (+ $i 1) (+ $j 1))) [ voteself = 1 ] ]
        guistrut 0.125
        guimerge 74 [
            guilist [
                guistrut 1.25
                guilist [ guistrut 4.5 ]
                votecolour = "^fw"
                if (= $voteself 1) [ votecolour = "^fy" ]
                guifont "default" [ guibutton (format "%1%2" $votecolour $voteplayers) ]
                guifont "radar" [ if (= $voteplayers 1) [ guibutton (format "%1vote" $votecolour) ] [  guibutton (format "%1votes" $votecolour) ] ]
            ]
            voteimage = "textures/conopen"
            votepath = (listfind curmap $maplist [(|| (strcmp $curmap $votemap) (strcmp (concatword "maps/" $curmap) $votemap) (&& (> $hasoctapaks 0) (> $mapocta 0) (strcmp (concatword "base/" $curmap) $votemap)))])
            if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
            guiimage $voteimage "" 2 1 "textures/conopen"
            guistrut 1
            guilist [
                guistrut 0.5
                gname = (gamename $votemode $votemuts)
                if (> (strlen $gname) 32) [ gname = (gamename $votemode $votemuts 1) ]
                guifont "sub" [ guibutton (format "^fy%1" $gname) ]
                guistrut 0.125
                guilist [
                    guistrut 2
                    guifont "radar" [ guibutton " ^favoted for on " ]
                    guifont "sub" [ guibutton (format "^fo%1" $votemap) ]
                ]
                guilist [
                    if (> $voteplayers 0) [
                        guifont "sub" [ guibutton "^fwby" ]
                        pname = ""
                        pmore = 0
                        loop j $voteplayers [
                            if (> (strlen $pname) 64) [ pmore = (+ $pmore 1) ] [
                                if (= $j 0) [ guibutton " " ] [
                                    if (= $j (- $voteplayers 1)) [
                                        pname = (format "%1 ^faand " $pname)
                                    ] [ pname = (format "%1^fa, " $pname) ]
                                ]
                                pname = (format "%1%2" $pname (getclientname (getvote (+ $i 1) (+ $j 1)) 1))
                            ]
                        ]
                        if (> $pmore 0) [ pname = (format "%1 ^faand ^fy%2 ^famore" $pname $pmore) ]
                        guifont "radar" [ guibutton $pname ]
                    ] [ guifont "sub" [ guibutton "^fano current votes" ] ]
                ]
            ]
        ] [
            if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]
        ] []
        guistrut 0.125
    ]
]

newgui maps [
    mapsmenuiter
    guilist [ mapsmenu ]
    guitab "votes"
    guilist [ votemenu ]
] [
    if (= $guipasses 0) [
        mapsmenuinit
        votemenuinit
    ]
]

varslist1 = [ maprotate spawnrotate selfdamage trialdamage teamdamage spawnhealth maxhealth maxhealthvampire actorscale minresizescale maxresizescale maxcarry regenguard regenhealth regenextra regenaffinity kamikaze pointlimit capturelimit captureresetdelay ]
varslist2 = [ defendlimit defendpoints defendoccupy defendflags bomberlimit bomberresetdelay bomberspeed gamespeed gravityforce liquidspeedforce liquidcurbforce floorcurbforce aircurbforce slidecurbforce movespeed movecrawl movesprint movehover movestraight movestrafe moveinair movestepup movestepdown jumpspeed impulsespeed impulselimit impulseboost ]
varslist3 = [ impulsedash impulsejump impulsemelee impulseparkour impulseparkournorm impulseallowed impulsestyle impulsecount impulsedelay impulseslide impulsemeter impulsecost impulseskate impulsesprint impulsehover impulseregen impulseregencrouch impulseregensprint impulseregenhover impulseregenmove impulseregeninair damagescale criticalchance ]
varslist4 = [ itemsallowed hitpushscale hitslowscale deadpushscale wavepushscale waveslowscale timelimit triallimit intermlimit votelimit duelreset duelclear duellimit itemspawntime itemspawndelay itemspawnstyle itemthreshold regendelay regentime enemyspawntime enemyspawndelay enemyspawnstyle spawngrenades spawnweapon instaweapon ]
varsmenu = [
    guifont "radar" [
        guilist [
            guilist [
                guilist [ looplist var $varslist1 [ vardef $var $var ] ]
                guistrut 2
                guilist [ looplist var $varslist1 [ varfld $var 8 ] ]
            ]
        ]
        guistrut 3
        guilist [
            guilist [
                guilist [ looplist var $varslist2 [ vardef $var $var ] ]
                guistrut 2
                guilist [ looplist var $varslist2 [ varfld $var 8 ] ]
            ]
        ]
        guistrut 3
        guilist [
            guilist [
                guilist [ looplist var $varslist3 [ vardef $var $var ] ]
                guistrut 2
                guilist [ looplist var $varslist3 [ varfld $var 8 ] ]
            ]
        ]
        guistrut 3
        guilist [
            guilist [
                guilist [ looplist var $varslist4 [ vardef $var $var ] ]
                guistrut 2
                guilist [ looplist var $varslist4 [ varfld $var 8 ] ]
            ]
            guistrut 0.5
            guistayopen [ guilist [ guistrut 4; guifont "radar" [ guibutton "^foreset all game variables" [resetvars] ] ] ]
        ]
    ]
]

weapcount = 10
weapindex = 0
weapmenuinit = [ weapindex = 0 ]
weapindexdelta = [
    weapindex = (+ $weapindex $arg1)
    if (< $weapindex 0) [ weapindex = (+ $weapindex $weapcount) ]
    if (>= $weapindex $weapcount) [ weapindex = (- $weapindex $weapcount) ]
]
weapmenu = [
    guistayopen [
        guilist [
            weapname = (getweap $weapindex -1)
            guilist [
                guiimage (getweap $weapindex 1) [weapindexdelta 1] 1.25 0 "textures/nothumb"
                guistrut 0.5
                guilist [
                    guifont default [ guitext (format "%1 (%2/%3)" $weapname $weapindex (+ $weapindex $weapcount)) ]
                    guilist [
                        guibutton "^fo<prev" [weapindexdelta -1]
                        guistrut 1
                        guibutton "^fynext>" [weapindexdelta 1]
                    ]
                ]
            ]
            guistrut 1
            guifont "radar" [
                guilist [
                    guilist [
                        vardef (format "%1name" $weapname) "name"
                        vardef (format "%1colour" $weapname) "colour"
                        vardef2 (format "%1partcol" $weapname) "partcol"
                        vardef2 (format "%1explcol" $weapname) "explcol"
                        vardef (format "%1add" $weapname) "add"
                        vardef (format "%1max" $weapname) "max"
                        vardef2 (format "%1sub" $weapname) "sub"
                        vardef2 (format "%1adelay" $weapname) "adelay"
                        vardef (format "%1rdelay" $weapname) "rdelay"
                        vardef2 (format "%1damage" $weapname) "damage"
                        vardef2 (format "%1speed" $weapname) "speed"
                        vardef2 (format "%1power" $weapname) "power"
                        vardef2 (format "%1time" $weapname) "time"
                        vardef2 (format "%1pdelay" $weapname) "pdelay"
                        vardef2 (format "%1gdelay" $weapname) "gdelay"
                        vardef2 (format "%1edelay" $weapname) "edelay"
                        vardef2 (format "%1explode" $weapname) "explode"
                    ]
                    guistrut 2
                    guilist [
                        varfld (format "%1name" $weapname) 13
                        varfld (format "%1colour" $weapname) 13
                        varfld2 (format "%1partcol" $weapname) 6
                        varfld2 (format "%1explcol" $weapname) 6
                        varfld (format "%1add" $weapname) 13
                        varfld (format "%1max" $weapname) 13
                        varfld2 (format "%1sub" $weapname) 6
                        varfld2 (format "%1adelay" $weapname) 6
                        varfld (format "%1rdelay" $weapname) 13
                        varfld2 (format "%1damage" $weapname) 6
                        varfld2 (format "%1speed" $weapname) 6
                        varfld2 (format "%1power" $weapname) 6
                        varfld2 (format "%1time" $weapname) 6
                        varfld2 (format "%1pdelay" $weapname) 6
                        varfld2 (format "%1gdelay" $weapname) 6
                        varfld2 (format "%1edelay" $weapname) 6
                        varfld2 (format "%1explode" $weapname) 6
                   ]
                ]
            ]
        ]
        guistrut 2
        guifont "radar" [
            guilist [
                guilist [
                    guilist [
                        vardef2 (format "%1rays" $weapname) "rays"
                        vardef2 (format "%1spread" $weapname) "spread"
                        vardef2 (format "%1zdiv" $weapname) "zdiv"
                        vardef2 (format "%1aiskew" $weapname) "aiskew"
                        vardef2 (format "%1flakweap" $weapname) "flakweap"
                        vardef2 (format "%1flakdmg" $weapname) "flakdmg"
                        vardef2 (format "%1flakrays" $weapname) "flakrays"
                        vardef2 (format "%1flaktime" $weapname) "flaktime"
                        vardef2 (format "%1flakspeed" $weapname) "flakspeed"
                        vardef2 (format "%1cooked" $weapname) "cooked"
                        vardef2 (format "%1guided" $weapname) "guided"
                        vardef2 (format "%1extinguish" $weapname) "extinguish"
                        vardef2 (format "%1radial" $weapname) "radial"
                        vardef2 (format "%1residual" $weapname) "residual"
                        vardef (format "%1reloads" $weapname) "reloads"
                        vardef (format "%1carried" $weapname) "carried"
                        vardef (format "%1zooms" $weapname) "zooms"
                        vardef2 (format "%1fullauto" $weapname) "fullauto"
                        vardef (format "%1allowed" $weapname) "allowed"
                        vardef2 (format "%1critdash" $weapname) "critdash"
                        vardef2 (format "%1taperin" $weapname) "taperin"
                        vardef2 (format "%1taperout" $weapname) "taperout"
                        vardef2 (format "%1elasticity" $weapname) "elasticity"
                        vardef2 (format "%1reflectivity" $weapname) "reflectivity"
                        vardef2 (format "%1relativity" $weapname) "relativity"
                    ]
                    guistrut 2
                    guilist [
                        varfld2 (format "%1rays" $weapname) 6
                        varfld2 (format "%1spread" $weapname) 6
                        varfld2 (format "%1zdiv" $weapname) 6
                        varfld2 (format "%1aiskew" $weapname) 6
                        varfld2 (format "%1flakweap" $weapname) 6
                        varfld2 (format "%1flakdmg" $weapname) 6
                        varfld2 (format "%1flakrays" $weapname) 6
                        varfld2 (format "%1flaktime" $weapname) 6
                        varfld2 (format "%1flakspeed" $weapname) 6
                        varfld2 (format "%1cooked" $weapname) 6
                        varfld2 (format "%1guided" $weapname) 6
                        varfld2 (format "%1extinguish" $weapname) 6
                        varfld2 (format "%1radial" $weapname) 6
                        varfld2 (format "%1residual" $weapname) 6
                        varfld (format "%1reloads" $weapname) 13
                        varfld (format "%1carried" $weapname) 13
                        varfld (format "%1zooms" $weapname) 13
                        varfld2 (format "%1fullauto" $weapname) 6
                        varfld (format "%1allowed" $weapname) 13
                        varfld2 (format "%1critdash" $weapname) 6
                        varfld2 (format "%1taperin" $weapname) 6
                        varfld2 (format "%1taperout" $weapname) 6
                        varfld2 (format "%1elasticity" $weapname) 6
                        varfld2 (format "%1reflectivity" $weapname) 6
                        varfld2 (format "%1relativity" $weapname) 6
                    ]
                ]
            ]
            guistrut 3
            guilist [
                guilist [
                    guilist [
                        vardef2 (format "%1waterfric" $weapname) "waterfric"
                        vardef2 (format "%1weight" $weapname) "weight"
                        vardef2 (format "%1radius" $weapname) "radius"
                        vardef2 (format "%1kickpush" $weapname) "kickpush"
                        vardef2 (format "%1hitpush" $weapname) "hitpush"
                        vardef2 (format "%1slow" $weapname) "slow"
                        vardef2 (format "%1aidist" $weapname) "aidist"
                        vardef2 (format "%1partsize" $weapname) "partsize"
                        vardef2 (format "%1partlen" $weapname) "partlen"
                        vardef (format "%1frequency" $weapname) "frequency"
                        vardef (format "%1pusharea" $weapname) "pusharea"
                        vardef (format "%1critmult" $weapname) "critmult"
                        vardef (format "%1critdist" $weapname) "critdist"
                        vardef2 (format "%1delta" $weapname) "delta"
                        vardef2 (format "%1trace" $weapname) "trace"
                        vardef2 (format "%1headmin" $weapname) "headmin"
                        vardef2 (format "%1whipdmg" $weapname) "whipdmg"
                        vardef2 (format "%1torsodmg" $weapname) "torsodmg"
                        vardef2 (format "%1legsdmg" $weapname) "legsdmg"
                        vardef2 (format "%1flakscale" $weapname) "flakscale"
                        vardef2 (format "%1flakspread" $weapname) "flakspread"
                        vardef2 (format "%1flakrel" $weapname) "flakrel"
                        vardef2 (format "%1flakffwd" $weapname) "flakffwd"
                        vardef2 (format "%1flakoffset" $weapname) "flakoffset"
                        vardef2 (format "%1flakskew" $weapname) "flakskew"
                    ]
                    guistrut 2
                    guilist [
                        varfld2 (format "%1waterfric" $weapname) 6
                        varfld2 (format "%1weight" $weapname) 6
                        varfld2 (format "%1radius" $weapname) 6
                        varfld2 (format "%1kickpush" $weapname) 6
                        varfld2 (format "%1hitpush" $weapname) 6
                        varfld2 (format "%1slow" $weapname) 6
                        varfld2 (format "%1aidist" $weapname) 6
                        varfld2 (format "%1partsize" $weapname) 6
                        varfld2 (format "%1partlen" $weapname) 6
                        varfld (format "%1frequency" $weapname) 13
                        varfld (format "%1pusharea" $weapname) 13
                        varfld (format "%1critmult" $weapname) 13
                        varfld (format "%1critdist" $weapname) 13
                        varfld2 (format "%1delta" $weapname) 6
                        varfld2 (format "%1trace" $weapname) 6
                        varfld2 (format "%1headmin" $weapname) 6
                        varfld2 (format "%1whipdmg" $weapname) 6
                        varfld2 (format "%1torsodmg" $weapname) 6
                        varfld2 (format "%1legsdmg" $weapname) 6
                        varfld2 (format "%1flakscale" $weapname) 6
                        varfld2 (format "%1flakspread" $weapname) 6
                        varfld2 (format "%1flakrel" $weapname) 6
                        varfld2 (format "%1flakffwd" $weapname) 6
                        varfld2 (format "%1flakoffset" $weapname) 6
                        varfld2 (format "%1flakskew" $weapname) 6
                    ]
                ]
            ]
            guistrut 3
            guilist [
                vardef2 (format "%1collide" $weapname) "^fwcollision:       ^fwpri^fa/^fwalt"
                guilist [
                    guilist [
                        guitext "impact geom"
                        guitext "bounce geom
                        guitext "impact ppl"
                        guitext "bounce ppl"
                        guitext "impact shot"
                        guitext "traced"
                        guitext "hit owner"
                        guitext "continue"
                        guitext "sticky"
                        guitext "destroy"
                     ]
                     guistrut 2
                     guilist [
                        varbit2 (format "%1collide" $weapname) 1
                        varbit2 (format "%1collide" $weapname) 2
                        varbit2 (format "%1collide" $weapname) 4
                        varbit2 (format "%1collide" $weapname) 8
                        varbit2 (format "%1collide" $weapname) 16
                        varbit2 (format "%1collide" $weapname) 32
                        varbit2 (format "%1collide" $weapname) 64
                        varbit2 (format "%1collide" $weapname) 128
                        varbit2 (format "%1collide" $weapname) 256
                        varbit2 (format "%1collide" $weapname) 512
                    ]
                ]
                guistrut 1
                vardef2 (format "%1flakcollide" $weapname) "^fwflak collision:  ^fwpri^fa/^fwalt"
                guilist [
                    guilist [
                        guitext "impact geom"
                        guitext "bounce geom
                        guitext "impact ppl"
                        guitext "bounce ppl"
                        guitext "impact shot"
                        guitext "traced"
                        guitext "hit owner"
                        guitext "continue"
                        guitext "sticky"
                        guitext "destroy"
                     ]
                     guistrut 2
                     guilist [
                        varbit2 (format "%1flakcollide" $weapname) 1
                        varbit2 (format "%1flakcollide" $weapname) 2
                        varbit2 (format "%1flakcollide" $weapname) 4
                        varbit2 (format "%1flakcollide" $weapname) 8
                        varbit2 (format "%1flakcollide" $weapname) 16
                        varbit2 (format "%1flakcollide" $weapname) 32
                        varbit2 (format "%1flakcollide" $weapname) 64
                        varbit2 (format "%1flakcollide" $weapname) 128
                        varbit2 (format "%1flakcollide" $weapname) 256
                        varbit2 (format "%1flakcollide" $weapname) 512
                    ]
                ]
            ]
        ]
    ]
]

newgui vars [
    guilist [ varsmenu ]
    guitab "weap"
    guilist [ weapmenu ]
] [
    if (= $guipasses 0) [
        weapmenuinit
    ]
]

sinfoindex = 0
sinfowait = 0
sinforetry = ""
sinfopass = ""
sinfonum = 0
sinfodisp = 0

servermenuinit = [
    sinfoindex = 0
    sinfowait = 0
    sinforetry = ""
    sinfopass = ""
    shownservergui = 0
]
servermenuiter = [
    if (!= $shownservergui 1) [
        maplist = ""
        mappath = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $maplist [strcmp $mcurmap $lcurmap]) 0) [
                maplist = (concat $maplist $lcurmap)
                mappath = (concat $mappath (concatword "maps/" $lcurmap))
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                maplist = (concat $maplist $lcurmap)
                mappath = (concat $mappath (concatword "base/" $lcurmap))
            ]
        ]
        shownservergui = 1
    ]
]
servermenu = [
    guipage sinfo 6 88 4.2 [> (updateservers) 0] (getserver 0) [sinfovalue = (getserver (+ $i 1))] [< (at $sinfovalue 0) 4] [guibutton "^fwThere are no servers to display, try ^fgupdating ^fwthe list" updatefrommaster [] "info"] [
        guilist [
            guibutton "^fgupdate"                       updatefrommaster;   guistrut 1.5
            guibutton "^frreset"                        clearservers;       guistrut 1.5
            guibutton "^fodisconnect"                   disconnect;         guistrut 1.5
            guibutton "^fmlan connect"                  lanconnect;         guistrut 1.5
            guicheckbox "^fcsearch lan"                 searchlan;          guistrut 1.5
            guibutton "^fyreset sort"                   serversortreset;    guistrut 4.5
            guitext (format "^fd[ ^fc%1 ^fwof ^fc%2" $sinfodisp $sinfonum)
            if (= $sinfonum 1) [ guitext " ^fwserver ^fd]" ] [ guitext " ^fwservers ^fd]" ]
        ]
        if (> (getversion 3) (getversion 1)) [
            guilist [ guibutton "^fzoynew version released! ^fwget it now from: ^fcwww.redeclipse.net" "" ]
        ]
    ] [
        sinfoattrs = (getserver (+ $i 1) 1)
        sinfostat = (at $sinfovalue 0)
        sinfoname = (at $sinfovalue 1)
        sinfoport = (at $sinfovalue 2)
        sinfonpid = (format "%1:[%2]" $sinfoname $sinfoport)
        sinfodesc = (at $sinfovalue 3)
        sinfomapn = (at $sinfovalue 4)
        sinfonump = (at $sinfovalue 5)
        sinfoping = (at $sinfovalue 6)
        sinfogver = (at $sinfoattrs 0)
        sinfomode = (at $sinfoattrs 1)
        sinfomuts = (at $sinfoattrs 2)
        sinfotime = (at $sinfoattrs 3)
        sinfomaxp = (at $sinfoattrs 4)
        sinfomstr = (at $sinfoattrs 5)
        sinfovars = (at $sinfoattrs 6)
        sinfomods = (at $sinfoattrs 7)
        guistrut 0.125
        guimerge 88 [
            guilist [
                guistrut 1
                guilist [ guistrut 8 ]
                if (< $sinfoping $serverwaiting) [
                    guilist [
                        guifont "emphasis" [ guibutton (format "^fw%1" $sinfonump) ]
                        guifont "radar" [ guibutton (format "^fd / ^fa%1" $sinfomaxp) ]
                    ]
                    guistrut 0.25
                    guilist [
                        guifont "sub" [ guibutton (format (if (< $sinfoping 200) [result "^fg%1"] [if (< $sinfoping 400) [result "^fy%1"] [result "^fr%1"]]) $sinfoping) ]
                        guifont "radar" [ guibutton " ^fams" ]
                    ]
                ] [
                    guifont "super" [ guibutton "?" ]
                ]
            ]
            sinfoimage = "textures/emblem"
            if (&& (< $sinfoping $serverwaiting) (= $sinfogver (getversion 1))) [
                sinfopath = (listfind curmap $maplist [(|| (strcmp $curmap $sinfomapn) (strcmp (concatword "maps/" $curmap) $sinfomapn) (&& (> $hasoctapaks 0) (strcmp (concatword "base/" $curmap) $sinfomapn)))])
                if (> $sinfopath -1) [ sinfoimage = (at $mappath $sinfopath) ]
            ]
            guiimage (format "<blur>%1" $sinfoimage) "" 2 1 "<blur>textures/emblem"
            guistrut 1
            guilist [
                guistrut 0.5
                guilist [
                    guifont "sub" [ guibutton (format "^fw%1" $sinfodesc) ]
                    guifont "radar" [
                        guibutton (format " ^fd(^fa%1^fd)" $sinfonpid)
                        if (> $sinfomods 0) [
                            guibutton (format " ^fc%1%% modified" (round (*f (divf $sinfomods $sinfovars) 100) 2))
                        ] [ guibutton " ^fgunmodified" ]
                    ]
                ]
                if (< $sinfoping $serverwaiting) [
                    guilist [
                        guistrut 0.25
                        if (= $sinfogver (getversion 1)) [
                            guifont "default" [
                                case $sinfostat 0 [
                                    guibutton "^fgopen" [] [] "textures/server"
                                ] 1 [
                                    guibutton "^fylock" [] [] "textures/serverlock"
                                ] 2 [
                                    guibutton "^fmpriv" [] [] "textures/serverpriv"
                                ] 3 [
                                    guibutton "^frfull" [] [] "textures/serverfull"
                                ] () [
                                    guibutton "^founknown" [] [] "textures/serverunk"
                                ]
                            ]
                            guifont "sub" [ guibutton (format " ^fd[^fw%1^fd]" (timetostr $sinfotime 2)) ]
                            gname = (gamename $sinfomode $sinfomuts)
                            if (> (strlen $gname) 32) [ gname = (gamename $sinfomode $sinfomuts 1) ]
                            guifont "radar" [ guibutton (format " ^fy%1 ^faon ^fo%2" $gname $sinfomapn) ]
                        ] [
                            guifont "default" [ guibutton "^foincompatible" [] [] "textures/serverfail" ]
                            guifont "sub" [ guibutton (format " ^faserver is using %1 protocol" (? (> $sinfogver (getversion 1)) "a ^fwnewer" "an ^fdolder")) ]
                        ]
                    ]
                    guilist [
                        if (strcmp $sinfonpid $sinforetry) [
                            if (= $sinfostat 3) [ guibutton "^fd[ ^fwwaiting for slot ^fd] " ]
                            guibutton "^fwpassword ^fd= "
                            sinfopassval = $sinfopass
                            guifield sinfopassval 20 [sinfopass = $sinfopassval]
                        ] [
                            if (> $sinfonump 0) [
                                guistrut 0.25
                                sinfoplayers = (getserver (+ $i 1) 2)
                                sinfopnum = (listlen $sinfoplayers)
                                if (> $sinfopnum 0) [
                                    guifont "sub" [ guibutton (format "^fc%1 ^fw%2:" $sinfopnum (? (= $sinfopnum 1) "player" "players")) ]
                                    pname = ""
                                    pmore = 0
                                    loop j $sinfopnum [
                                        if (> (strlen $pname) 86) [ pmore = (+ $pmore 1) ] [
                                            if (= $j 0) [ guibutton " " ] [
                                                if (= $j (- $sinfopnum 1)) [
                                                    pname = (format "%1 ^faand " $pname)
                                                ] [ pname = (format "%1^fa, " $pname) ]
                                            ]
                                            pname = (format "%1%2" $pname (at $sinfoplayers $j))
                                        ]
                                    ]
                                    if (> $pmore 0) [ pname = (format "%1 ^faand ^fy%2 ^famore" $pname $pmore) ]
                                    guifont "radar" [ guibutton $pname ]
                                ] [ guifont "sub" [ guibutton "^faplayer info not available" ] ]
                            ] [ guifont "sub" [ guibutton "^fano players online" ] ]
                        ]
                    ]
                ] [
                    guilist [
                        guistrut 0.25
                        guifont "default" [ guibutton "^founresponsive" [] [] "textures/serverfail" ]
                        guifont "sub" [ guibutton " ^faserver is not replying to queries" ]
                    ]
                ]
            ]
        ] [
            sinfopass = ""
            sinforetry = @sinfonpid
            if (|| (hasauthkey) (!= @sinfomstr 4)) [ sinfowait = 0 ] [ sinfowait = 1 ] 
        ] [
            sinfopass = ""
            sinforetry = @sinfonpid
            if (hasauthkey) [ sinfowait = 0 ] [ sinfowait = 1 ] 
        ]
        if (strcmp $sinfonpid $sinforetry) [
            if (= $sinfowait 1) [
                if (> (strlen $sinfopass) 0) [ connect $sinfoname $sinfoport $sinfopass; sinfopass = ""; sinforetry = ""; sinfowait = 0 ]
            ] [
                if (|| (hasauthkey) (!= $sinfostat 3)) [ connect $sinfoname $sinfoport; sinfopass = ""; sinforetry = ""; sinfowait = 0 ]
            ]
        ]
        guistrut 0.125
    ] [
        guicomb [
            guistrut 0.25
            guilist [
                guifont "radar" [ guitext "update interval:" ]
                guistrut 0.5
                guilist [
                    guistrut 0.25
                    guilist [ guistrut 75 ]
                    guilistslider serverupdateinterval "1 2 3 4 5 10 15 20 25 30 35 40 45 50 55 60"
                ]
            ]
        ]
    ]
]

newgui servers [
    servermenuiter
    guilist [ servermenu ]
] [
    if (= $guipasses 0) [
        servermenuinit
    ]
]

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
]

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]

doinsel = 0

findents = [
    match = 1
    if $doinsel [
        if (= $doinsel 1) [
            match = (* $match (insel))
        ] [
            match = (* $match (! (insel)))
        ]
    ]
    if $dosearch0 [
        match = (* $match (strcmp (et) (at $enttypelist $entt)))
    ]
    loop i 5 [
        if (getalias (concatword "dosearch" (+ $i 1))) [
            match = (* $match (= (ea (+ $i 1)) (getalias (concatword "enta" (+ $i 1)))))
        ]
    ]

    result $match
]

entt = 0
enta1 = 0
enta2 = 0
enta3 = 0
enta4 = 0
enta5 = 0

dosearch0 = 1
dosearch1 = 1
dosearch2 = 1
dosearch3 = 1
dosearch4 = 1
dosearch5 = 1

newgui entfind [
    guitext "Set the attributes below to search for"
    guistrut 1
    guicheckbox "Type (see next tab)" dosearch0
    loop i 5 [
        guilist [
            guicheckbox [attr @(+ $i 1)] [dosearch@(+ $i 1)]
            guistrut 1
            guifield [enta@(+ $i 1)] 11 [ enta@(+ $i 1) = (min 0x7FFFFFFF (max -0x7FFFFFFF $enta@(+ $i 1)))]
        ]
    ]

    guilist [
        guiradio "off" doinsel 0
        guistrut 1
        guiradio "inside selection" doinsel 1
        guistrut 1
        guiradio "outside selection" doinsel 2
    ]

    guibar
    guistayopen [
        guibutton "Copy Attributes from currently selected entity" [
            loop i 5 [
                [enta@(+ $i 1)] = (ea (+ $i 1))
            ]
            loop i (listlen $enttypelist) [
                if (strcmp (et) (at $enttypelist $i)) [
                    entt = $i
                ]
            ]
        ]
        guibutton "select all valid entities" [entselect [(findents)]]
    ]

    guitab Type
    guilist [
        loop i (+ (div (listlen $enttypelist) 10) 1) [
            guilist [
                loop j (min (- (listlen $enttypelist) (* 10 $i)) 10) [
                    guiradio (at $enttypelist (+ $j (* $i 10))) entt (+ $j (* $i 10))
                ]
            ]
        ]
    ]
]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "find entities"               "showgui findent"
    guibutton "toggle edit mode (key F1)"              "edittoggle"
    guibutton "toggle heightmap (key H)"
    guibutton "deselect     (key SPACE)"                "cancelsel"
    guibutton "passthrough  (hold ALT)"               "passthrough"
    guibutton "select       drag left mouse   "
    guibutton "select corners   drag middle mouse "
    guibutton "extend selection right mouse"
    guibutton "reorient     right mouse"
    guibutton "move selection   drag right mouse"
    guibutton "copy     (key C)"                              "copy"
    guibutton "paste        (key V)"                            "paste"

    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"

        guibutton "newent playerstart   "
        guibutton "newent weapon"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
      ]
    ]

    guitab keys
    guibutton "flip / mirror cube   (key X)"            "flip"
    guibutton "undo     (key Z, U)"                     "undo"
    guibutton "redo     (key I)"                        "redo"
    guibutton "delete       (BACKSPACE)"                  "editdel"
    guibutton "texture palette     (F2)" "showtexgui"
    guibutton "mapmodels list           (F4)" "showgui mapmodels"
    guibutton "select all in box    (N)" "entselect insel"
    guibutton "select all matching  (M)" "selentfindall"
    guibutton "center view on ent   (COMMA + scroll)" "entautoview"
    guibutton "edit entity console  (PERIOD)" "selentedit"
    guibutton "toggle heightmap tex (MMB in heightmapmode)"
    guicheckbox "toggle outline     (key 7)"        outline
    guicheckbox "toggle wireframe       (key 8)"    wireframe


    guitab scroll
    guitext "push/pull selection        scroll wheel only"
    guitext "push the face of a cube    hold F + scroll wheel"
    guitext "change gridsize        hold G + scroll wheel"
    guitext "change textures        hold Y + scroll wheel"
    guitext "rotate                 hold R + scroll wheel"
    guitext "push corner with cursor    hold Q + scroll wheel"
    guitext "push/pull heightmap                hold LCTRL + scroll wheel"
    guitext "change heightmap brush hold B + scroll wheel"

    guitab light

    guibutton "recompute lights (slow: 8xAA)" "calclight 1"
    guibutton "recompute lights (quick: no AA, no model shadows)"   "calclight -1"
    guibutton "patch lighting"                              "patchlight"

    guilist [
        guicheckbox "cast shadows (*)" lmshadows 2
        guicheckbox "fullbright" fullbright
    ]

    guitext "lightprecision (default: 32)"
    guislider lightprecision

    guitext "lighterror (default: 8)"
    guislider lighterror

    guitab mapop
    guitext   Savemap:
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/mapmsg ]"

    guitab cfg
    guitext (format "%1.cfg" $mapname)
    showfileeditor (format "%1.cfg" $mapname) -50 12
]

newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "aiclip"                     "editmat aiclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"

    guicheckbox "show material volumes"    showmat
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

resetlight = [
    lightcolour = 0
    lightrgb = "255 255 255"
    lightbright = 1
    lightradius = 128
]

lightcmd = [
    lightr = (at $lightrgb 0)
    lightg = (at $lightrgb 1)
    lightb = (at $lightrgb 2)
    if (= $lightbright 0) [
        if (strcmp $lightrgb "255 255 255") [
            lightr = 192; lightg = 192; lightb = 192
        ] [
            lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
         ]
    ]
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 200 100"
    guibutton (lightcmd)
    guibar
    guicheckbox "bright"    lightbright
    guitext "color:"
    guiradio "white"        lightcolour 0 [lightrgb = "255 255 255"]
    guiradio "blue"         lightcolour 1 [lightrgb = "192 192 255"]
    guiradio "red"          lightcolour 2 [lightrgb = "255 192 192"]
    guiradio "green"        lightcolour 3 [lightrgb = "192 255 192"]
    guiradio "yellow"       lightcolour 4 [lightrgb = "255 255 192"]
    guiradio "purple"       lightcolour 5 [lightrgb = "255 192 255"]
    guiradio "turquoise"    lightcolour 6 [lightrgb = "192 255 255"]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" [] (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radioboxon"] [result "radiobox"])
]

newgui options [
    guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guibar
    if (&& (< $shaders 0) (= $renderpath 0)) [
        guicheckbox "^f2shaders         "           shaders 1 -1
    ] [
        guilist [
            guicheckbox "^f2shaders			"           shaders
            if $shaders [
                guibar
                guiradio "^f0low detail" shaderdetail 1
                guibar
                guiradio "^f2high detail" shaderdetail 3
                //if $hasglsl [
                //    guibar
                //    guicheckbox "^f3GLSL only" forceglsl
                //]
            ]
        ]
    ]
    guilist [
        guitext "water			" blank
        guibar
        guicheckbox "^f2refraction"  waterrefract
        guibar
        guicheckbox "^f3reflection"  waterreflect
        guibar
        guicheckbox "^f0caustics"    caustics
    ]
    if (> $renderpath 0) [
        guilist [
            guitext "waterfalls		" blank
            guibar
            guicheckbox "^f2refraction" waterfallrefract
            guibar
            guicheckbox "^f0reflection" waterfallenv
        ]
    ]
    if (= $renderpath 0) [
        guilist [
            guicheckbox "^f3shadow maps	"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1]
                guibar
                guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^f0blob shadows" blobs
            ]
        ]
        if (>= $maxtmus 3) [
            guicheckbox "^f2dynamic lights"    ffdynlights 5 0
        ]
    ] [
        guilist [
            guicheckbox "^f3soft shadows	"  shadowmap
            if $shadowmap [
                guibar
                guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1]
                guibar
                guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2]
            ] [
                guibar
                guicheckbox "^f0blob shadows" blobs
            ]
        ]
        if $glare [
            glarepreset = 0
            if (= $glarescale 1) [
                if (= $blurglare 4) [glarepreset = 1]
                if (= $blurglare 7) [glarepreset = 3]
            ]
            if (= $glarescale 2) [
                if (= $blurglare 3) [glarepreset = 2]
                if (= $blurglare 7) [glarepreset = 4]
            ]
            guilist [
                guicheckbox "^f3glare			"       glare
                guibar
                guiradio "^f2subtle" glarepreset 1 [blurglare 4; glarescale 1]
                guibar
                guiradio "^f2glowy" glarepreset 2 [blurglare 3; glarescale 2]
                guibar
                guiradio "^f3soft" glarepreset 3 [blurglare 7; glarescale 1]
                guibar
                guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2]
            ]
        ] [
            guicheckbox "^f3glare"         glare
        ]
    ]
    if $usetexrect [
        guilist [
            guicheckbox "^f3motion blur		" motionblur
            if $motionblur [
                guibar
                guiradio "^f3subtle" motionblurscale 0.5
                guibar
                guiradio "^f3moderate" motionblurscale 1
            ]
        ]
    ]
    guilist [
        guicheckbox "^f3grass			" grass
        if $grass [
            guibar
            guiradio "^f2quick fade" grassdist 128
            guibar
            guiradio "^f2moderate fade" grassdist 256
            guibar
            guiradio "^f3slow fade" grassdist 512
        ]
    ]
    if (> $renderpath 0) [
        guilist [
            guicheckbox "^f0dynamic lights	"    maxdynlights 3 0
            if $maxdynlights [
                guibar
                guiradio "^f0medium quality" maxdynlights 3
                guibar
                guiradio "^f2high quality" maxdynlights 5
            ]
        ]
        guilist [
            guicheckbox "^f2soft particles	" depthfx
            if $depthfx [
                guibar
                guiradio "^f0low quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
                guibar
                guiradio "^f2medium quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
                guibar
                guiradio "^f3high quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
            ]
        ]
    ]
    guicheckbox "^f0glass reflection"  glassenv
    guilist [
        guicheckbox "^f0decals			" decals
        if $decals [
            guibar
            guiradio "^f0medium quality" maxdecaltris 1024
            guibar
            guiradio "^f2high quality" maxdecaltris 4096
        ]
    ]
    guicheckbox "^f0fix t-joints (world sparklies)" filltjoints
    guilist [
        guitext "textures			" blank
        guibar
        guiradio "^f0low quality" maxtexsize 256
        guibar
        guiradio "^f2high quality" maxtexsize 0
    ]
    guilist [
        guitext "models			" blank
        guibar
        guicheckbox "^f0lighting"    lightmodels
        guibar
        guicheckbox "^f0reflection"  envmapmodels
        guibar
        guicheckbox "^f0glow"        glowmodels
        if (> $renderpath 0) [
            guibar
            guicheckbox "^f2bumpmap" bumpmodels
        ]
    ]
    guilist [
        guitext "animation		" blank
        guibar
        guiradio "^f0medium quality" matskel 1
        guibar
        guiradio "^f0high quality" matskel 0
    ]

    guitab    "display"
    guicheckbox "v-sync"            vsync 1 0
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "color depth (default: 0)"
    guilistslider colorbits "0 8 16 24 32"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 8 16 24 32"
    guitext "stencil bits (default: 0)"
    guislider stencilbits
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"

    guitab   "ui"
	guilist [
		guicheckbox "show crosshair " showcrosshair
		guicheckbox "weapon crosshairs " crosshairweapons
	]
    guilist [
		guicheckbox "show clips " showclips
		guicheckbox "show radar " showradar
	]
    guibar
    guilist [
      guitext "crosshair size: "
      crosshairsizestorage = $crosshairsize
      guifield crosshairsizestorage 6 [crosshairsize $crosshairsizestorage]
      guitext " (0 - 1000, default: 0.05)"
    ]
    guilist [
      guitext "zoom crosshair size: "
      zoomcrosshairsizestorage = $zoomcrosshairsize
      guifield zoomcrosshairsizestorage 6 [zoomcrosshairsize $zoomcrosshairsizestorage]
      guitext " (0 - 1000, default: 0.575)"
    ]
    guibar
    guitext "notice messages"
    guilist [
    guilist [
      guiradio "off" shownotices 0
      guiradio "basic notices" shownotices 1
      guiradio "level 2" shownotices 2
      guiradio "level 3" shownotices 3
      guiradio "everything" shownotices 4
    ]
    guibar
    guilist [
      guitext "fps display"
      guiradio "off" showfps 0
      guiradio "number only" showfps 1
      guiradio "verbose" showfps 2
      guiradio "more verbose" showfps 3
    ]
    ]

    guitab   "res"
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab   "sound"
    guitext "master volume"
    guislider mastervol
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guicheckbox "in-game music" musictype
    guicheckbox "mumble positional audio" mumble

    guitab  "mouse"
    guilist [
        guilist [
            guitext "mouse input style "
            guitext "mouse overall sensitivity  "
            guitext "mouse yaw sensitivity"
            guitext "mouse pitch sensitivity"
            guistrut 1
            guicheckbox "invert y-axis" mouseinvert
        ]
        guilist [
            guilist [
                guiradio "default " mousetype 0
                guiradio "panned " mousetype 1
                guiradio "free " mousetype 2
            ]
            guislider sensitivity 1 100
            guislider yawsensitivity 0 100
            guislider pitchsensitivity 1 100
            guistrut 1
            guicheckbox "absolute movements" mouseabsolute
        ]
    ]

    guitab  "keys"
    guitext "basic keybinds, for anything more use the 'bind' command"
    guitext "select action to bind and press desired keys (ESC when done):"
    guistrut 1
    guilistsplit n 2 $bindactions [
        guilist [
            guitext (tabify (concatword (at $bindnames $i) " ") 4)
            [newbinds@i] = (searchbinds $n)
            guikeyfield [newbinds@i] -24 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@i] [bind $j [@@@n]]
            ]
        ]
    ] [guistrut 2]
    
    //guitab "autoexec.cfg"
    //guitext "autoexec.cfg"
    //showfileeditor "autoexec.cfg" -60 15
]

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "bloom (subtle: 30%)"    "bloom 0.3"
    guibutton "bloom (bright: 55%)"    "bloom 0.55"
    guibutton "bloom (intense: 80%)"  "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

bindactions = [
    forward backward left right "universaldelta 1" "universaldelta -1" "spectator 1" "spectator 0"
    "action 0" "action 1" "action 2" "action 3" "action 4" "action 5" "action 6" "action 7" "action 8" "action 9"
    "weapon 0" "weapon 1" "weapon 2" "weapon 3" "weapon 4" "weapon 5" "weapon 6" "weapon 7" "weapon 8" "weapon 9"
    "saycommand /" saytextcommand sayteamcommand toggleconsole edittoggle takescreenshot
    addbot delbot "showgui maps 1" "showgui maps 2" "setmaster 1" "showgui loadout" "showgui team"
    "showcompass voice" "showcompass team"
]
bindnames = [
    forward backward left right "scroll up" "scroll down" "enter spectator" "exit spectator"
    "attack" "alt-attack" "reload" "use" "jump" "run/walk" "crouch" "special" "drop" "affinity"
    "melee" "pistol" "sword" "shotgun" "smg" "flamer" "plasma" "rifle" "grenade" "rocket"
    "cmd input" "all chat" "team chat" "toggle console" "toggle editing" "screenshot"
    "add bot" "delete bot" "maps menu" "maps voting" "claim master" "loadout menu" "team menu"
    "voice compass" "team compass"
]

entupdate = [ entset $tmpt $tmp0 $tmp1 $tmp2 $tmp3 ]

initentgui = [
  tmpt = ( et )
  tmp0 = ( ea 0 )
  tmp1 = ( ea 1 )
  tmp2 = ( ea 2 )
  tmp3 = ( ea 3 )
]

genentattributes = [
    entattributes = ""
    n = (listlen $arg2)
    loop i $n [
        entattributes = (concat $entattributes [
            guitext @(at $arg2 $i)
            guislider tmp@i @(at $arg3 (* 2 $i)) @(at $arg3 (+ 1 (* 2 $i))) entupdate
        ])
    ]
]

guilistsplit = [
  guilist [
    i = 0
    l = (listlen $arg3)
    z = (div (+ $l (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $l)
        while [< $i $t] [
          $arg1 = (at $arg3 $i)
          arg4
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton quicklight          "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent light"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
  guibutton "air"                        "editmat air"
  guibutton "water"                      "editmat water"
  guibutton "clip"                       "editmat clip"
  guibutton "glass"                      "editmat glass"
  guibutton "noclip"                     "editmat noclip"
  guibutton "lava"                       "editmat lava"
  guibutton "aiclip"                     "editmat aiclip"
  guibutton "death"                      "editmat death"
  guibutton "alpha"                      "editmat alpha"
  guibar
  guicheckbox "show material volumes"    showmat
]

newentgui = [
  genentattributes [@arg1] [@arg2] [@arg3]
  newgui $arg1 [
    guitext $tmpt
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab misc
    @quickeditmenu
  ]
]

loop i $enttypelength [
  newentgui (at $enttypelist $i) "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "yaw team id" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "yaw tag" "0 360 0 20"
newentgui monster "yaw type" "0 360 0 7"
newentgui mapmodel "model yaw type tag flags" "360 0 0 100 0 29 0 20"
newentgui envmap "radius" "0 400"
newentgui pusher "Z Y X" "0 200 0 200 0 200"
newentgui carrot "tag type" "0 50 0 29"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

guilistloop = [
    i = 0
    j = (+ (div $arg3 (* $arg1 $arg2)) 1)
    loop a $j [
        if (> $a 0) [ guitab $a ]
        guilist [
            loop b $arg1 [
                guilist [
                    loop c $arg2 [
                        if (< $i $arg3) [
                            arg4; i = (+ $i 1)
                            if (< $c (- $arg2 1)) [ guibar ]
                        ]
                    ]
                ]
                if (&& (< $i $arg3) (< $b (- $arg1 1))) [ guibar ]
            ]
        ]
    ]
]

alias showmapmodel [
    mapmodelpath = (mapmodelindex $arg1)
    mapmodelshot = (concatword "models/" $mapmodelpath "/thumb")
    mapmodelcmd = (concat newent mapmodel $arg1)
    guilist [ guilist [
        guiimage $mapmodelshot $mapmodelcmd 2 1 "textures/nothumb"
        guibutton $mapmodelpath $mapmodelcmd
    ] ]
]

newgui mapmodels [
    mapmodelnum = (mapmodelindex)
    guilistloop 4 2 $mapmodelnum [ showmapmodel $i ]
]
